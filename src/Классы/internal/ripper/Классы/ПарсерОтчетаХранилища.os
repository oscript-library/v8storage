#Использовать datetime

// Выполняет чтение файла отчета в таблицу версий
//
// Параметры:
//   ПутьКФайлу - Строка - путь к файлу отчета из хранилища
//
// Возвращаемое значение:
//   ТаблицаЗначений - колонки:
//      Номер   - число - номер версии
//      Дата    - Дата - Дата версии
//      Автор   - строка - автор версии
//      Комментарий   - Строка - многострочная строка с комментарием к версии
//
Функция ПрочитатьФайлОтчетаХранилища(Знач ПутьКФайлу) Экспорт
	
	ТекстФайла = ПрочитатьФайл(ПутьКФайлу);

	Возврат ПрочитатьТекстСкобкоФайлаРегулярками(ТекстФайла);

КонецФункции


Функция ПрочитатьТекстСкобкоФайлаРегулярками(Знач Текст)
	
	Текст = СтрЗаменить(Текст, """""", "'");

	РегВыражение = Новый РегулярноеВыражение("[\{]""#"",""([^""]+)[""][\}]");
	МассивСовпадений = РегВыражение.НайтиСовпадения(Текст);

	Массив = Новый Массив();

	Для каждого Совпадение Из МассивСовпадений Цикл
		Массив.Добавить(Совпадение.Группы[1].Значение);
	КонецЦикла;

	ТаблицаВерсий = СформироватьТаблицуВерсий(Массив);

	Возврат ТаблицаВерсий;

КонецФункции

Функция ПрочитатьТекстСкобкоФайла(Текст)
	
	ЧитательСкобкоФайла = Новый ripper;

	НачальныйМассив = ЧитательСкобкоФайла.Разобрать(Текст);

	ПослеОбхода = РекурсивныйОбход(НачальныйМассив[0]);
	
	ТаблицаВерсий = СформироватьТаблицуВерсий(ПослеОбхода);
	
	Возврат ТаблицаВерсий;

КонецФункции

Функция ПрочитатьФайл(Знач ПутьКФайлу) 
	
	ЧтениеТекста = Новый ЧтениеТекста(ПутьКФайлу, "UTF-8");
	ТекстФайла = ЧтениеТекста.Прочитать();
	ЧтениеТекста.Закрыть();

	Возврат ТекстФайла;

КонецФункции


Функция РекурсивныйОбход(Элемент);

	Если ТипЗнч(Элемент) = Тип("Массив") Тогда
		
		НовыйМассив = Новый Массив;
		Для Каждого Стр Из Элемент Цикл
			
			ЭначениеСтр = РекурсивныйОбход(Стр);
			
			Если ЭначениеСтр = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
		    НовыйМассив.Добавить(ЭначениеСтр);
			
		КонецЦикла; 
		
		Если НовыйМассив.Количество() = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Если НовыйМассив.Количество() = 1 Тогда
			Возврат НовыйМассив[0];
		КонецЕсли;
		
		Возврат НовыйМассив;
		
	ИначеЕсли ТипЗнч(Элемент) = Тип("Соответствие")
		ИЛИ ТипЗнч(Элемент) = Тип("Структура") Тогда
		
		Для Каждого Стр Из Элемент Цикл
			
			Стр.Значение = РекурсивныйОбход(Стр.Значение);
			
		КонецЦикла; 
		
		Возврат УбратьКавычки(Элемент);
		
	ИначеЕсли СтрНачинаетсяС(Элемент, """#""") Тогда
		
		Возврат Неопределено;
		
		
	ИначеЕсли Не СтрНачинаетсяС(Элемент, """") Тогда
		
		Возврат Неопределено;
		
	Иначе
		
		Возврат УбратьКавычки(Элемент);
		
	КонецЕсли;
	
КонецФункции

Функция ПолучитьТаблицуВерсий()
	
    ТаблицаВерсий = Новый ТаблицаЗначений;
    ТаблицаВерсий.Колонки.Добавить("Номер");
    ТаблицаВерсий.Колонки.Добавить("Дата");
    ТаблицаВерсий.Колонки.Добавить("Время");
    ТаблицаВерсий.Колонки.Добавить("Автор");
    ТаблицаВерсий.Колонки.Добавить("Комментарий");
    ТаблицаВерсий.Колонки.Добавить("Изменены");
    ТаблицаВерсий.Колонки.Добавить("Добавлены");
 	ТаблицаВерсий.Колонки.Добавить("Удалены");

	Возврат ТаблицаВерсий;

КонецФункции

Функция СформироватьТаблицуВерсий(Массив) 
	
	ТаблицуВерсий = ПолучитьТаблицуВерсий();
	СтрокаТаблицы = Неопределено;
	ИмяПоля = "";
	Для ИИ = 0 По Массив.ВГраница() Цикл
		
		ТекущаяСтрока = Массив[ИИ];
		                  		
		Если СтрНачинаетсяС(ТекущаяСтрока, "Версия:") Тогда
			
			СтрокаТаблицы = ТаблицуВерсий.Добавить();
		    СтрокаТаблицы.Изменены = Новый Массив;
			СтрокаТаблицы.Добавлены = Новый Массив;
			СтрокаТаблицы.Удалены = Новый Массив;
			ИмяПоля = "Номер";
			
		ИначеЕсли СтрНачинаетсяС(ТекущаяСтрока, "Пользователь:") Тогда
			ИмяПоля = "Автор";
		ИначеЕсли СтрНачинаетсяС(ТекущаяСтрока, "Дата создания:") Тогда
	 		ИмяПоля = "Дата";
		ИначеЕсли СтрНачинаетсяС(ТекущаяСтрока, "Время создания:") Тогда
	 		ИмяПоля = "Время";
		ИначеЕсли СтрНачинаетсяС(ТекущаяСтрока, "Комментарий:") Тогда
	 		ИмяПоля = "Комментарий";
		ИначеЕсли СтрНачинаетсяС(ТекущаяСтрока, "Изменены:") Тогда
	 		ИмяПоля = "Изменены";
		ИначеЕсли СтрНачинаетсяС(ТекущаяСтрока, "Добавлены:") Тогда
	 		ИмяПоля = "Добавлены";
		ИначеЕсли СтрНачинаетсяС(ТекущаяСтрока, "Удалены:") Тогда
	 		ИмяПоля = "Удалены";
		Иначе 
			
			Если НЕ СтрокаТаблицы = Неопределено Тогда
				
				Если ИмяПоля = "Изменены" 
					Или ИмяПоля = "Добавлены"
					ИЛИ ИмяПоля = "Удалены" Тогда
					
					СтрокаТаблицы[ИмяПоля].Добавить(ТекущаяСтрока);
				

					
				ИначеЕсли ИмяПоля = "Комментарий" Тогда 
					
					СтрокаТаблицы[ИмяПоля] = СократитьКавычки(ТекущаяСтрока);
						
				Иначе
					
					СтрокаТаблицы[ИмяПоля] = ТекущаяСтрока;
				    
				Конецесли;
				
				
			КонецЕсли;
			
		КонецЕсли;		
		
	КонецЦикла;
	
	Для Каждого ОписаниеВерсии Из ТаблицуВерсий Цикл
		
		ОписаниеВерсии.Номер = Число(СтрЗаменить(ОписаниеВерсии.Номер, Символы.НПП, ""));
		ВремяВСекундах = ВремяВСекундах(ОписаниеВерсии.Время);
		ОписаниеВерсии.Дата = РаботаСДатой.СтрокаВДату(ОписаниеВерсии.Дата, "dd.MM.yyyy") + ВремяВСекундах;
		
	КонецЦикла;

	Возврат ТаблицуВерсий;
	
КонецФункции

Функция ВремяВСекундах(Время)
	
	МассивЧисел = СтрРазделить(Время, ":");
	Часы = МассивЧисел[0];
	Минуты = МассивЧисел[1];
	Секунды = МассивЧисел[2];

	Возврат Часы * 3600 + Минуты*60 + Секунды;

КонецФункции

Функция УбратьКавычки(Знач СтрокаДанных)
	
	Если СтрНачинаетсяС(СтрокаДанных, """") Тогда 
		
		СтрокаДанных = Сред(СтрокаДанных,2);
		
	КонецЕсли;	
	
	Если СтрЗаканчиваетсяНа(СтрокаДанных, """") Тогда 
		
		СтрокаДанных = Лев(СтрокаДанных, СтрДлина(СтрокаДанных) -1 );
		
	КонецЕсли;	
	
	Возврат СтрокаДанных;
	
КонецФункции

Функция СократитьКавычки(Знач СтрокаДанных)

	Возврат СтрЗаменить(СтрокаДанных, """""", """");

КонецФункции